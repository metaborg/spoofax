module sdf2imp/util/origin-tracking

rules //origin primitives

origin-sublist-term = prim("SSL_EXT_origin_sublist_term", <id>)

origin-term = 
	prim("SSL_EXT_origin_term", <id>) <+ 
	origin-sublist-term

origin-term-desugared = prim("SSL_EXT_origin_term_desugared", <id>)
origin-term-fuzzy = prim("SSL_EXT_origin_term_fuzzy", <id>)

origin-text   = origin-support-sublist(prim("SSL_EXT_origin_text", <id>))
origin-offset = origin-support-sublist(prim("SSL_EXT_origin_offset", <id>)) 
origin-indent = origin-support-sublist(prim("SSL_EXT_origin_indentation", <id>))
origin-separation = origin-support-sublist(prim("SSL_EXT_origin_separation", <id>)) //only succeeds if list >=2 elems
origin-layout-prefix = origin-support-sublist(prim("SSL_EXT_origin_layout_prefix", <id>))

origin-deletion-offset = 
	origin-support-sublist(prim("SSL_EXT_origin_deletion_offset", <id>)); 
	!(<Fst>, <Snd; inc>) //exclusive end

origin-comments-after = origin-support-sublist(prim("SSL_EXT_origin_comments_after", <id>))
origin-insert-before-offset = origin-support-sublist(prim("SSL_EXT_origin_insert_before_offset", <id>))
origin-insert-at-end-offset = origin-support-sublist(prim("SSL_EXT_origin_insert_at_end_offset", <id>))
origin-text-with-layout = origin-support-sublist(prim("SSL_EXT_origin_text_with_layout", <id>))

rules //origin strategies

origin-start-offset = origin-offset; Fst
origin-end-offset = origin-offset; Snd
origin-deletion-start-offset = origin-deletion-offset; Fst 
origin-deletion-end-offset = origin-deletion-offset; Snd
origin-layout-prefix-start-offset = origin-insert-before-offset

is-origin-term = <equal> (<origin-term>, <id>)
is-origin-term-desugared = <equal> (<origin-term-desugared>, <id>)
has-origin-term = where(origin-term)
has-origin-sublist-term = where(origin-sublist-term)
is-origin-sublist-term = <equal> (<origin-sublist-term>, <id>)

origin-support-sublist(s) =
	(origin-sublist-term; s) <+ 
	(?[h|[]]; <s> h) <+
	s

origin-set-relation:
	(origin-node, new-node) -> origin-tracked-node
	where
		origin-tracked-node := <!Some(<id>); all(!new-node); ?Some(<id>)> origin-node

origin-track-forced(s) =
	![<id>]; all(s); ?[<id>]

rules //support default values for non-origin elements
			
TABSIZE = !4
DEFAULT-INDENT = !"\t" //HACK: this is used in case multiline subterm texts are pretty-printed

layout-prefix = origin-layout-prefix <+ !""
comments-after = origin-comments-after <+ !""